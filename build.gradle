buildscript {
    repositories {
        maven {
            url 'https://repo.ronella.xyz/artifactory/java-central'
            credentials {
                username "${artifactoryUsername}"
                password "${artifactoryPassword}"
            }
            mavenContent {
                releasesOnly()
            }
        }
        maven {
            url 'https://repo.ronella.xyz/archiva/repository/snapshots/'
            credentials {
                username "${archivaUsername}"
                password "${archivaPassword}"
            }
            mavenContent {
                snapshotsOnly()
            }
        }
    }

    dependencies {
        classpath fileTree(dir: './libs/plugins', include: ['*.jar'])
    }
}

plugins {
    id 'groovy'
    id 'java-gradle-plugin'
    id 'com.gradle.plugin-publish' version '0.11.0'
    id 'maven-publish'
}

ext {
    publishingPath = version.toString().contains("SNAPSHOT") ? 'snapshots' : 'internal'
}

pluginBundle {
    website = 'https://github.com/rcw3bb/gs-codenarc'
    vcsUrl = 'https://github.com/rcw3bb/gs-codenarc'
    tags = ['ronella', 'gosu', 'codenarc', 'gs-codenarc', 'quality', 'code-quality']
}

gradlePlugin {
    plugins {
        gsCodeNarcPlugin {
            id = 'xyz.ronella.gs-codenarc'
            displayName = 'GS CodeNarc Plugin'
            description = 'A Gosu extension for CodeNarc'
            implementationClass = 'xyz.ronella.gradle.plugin.gosu.codenarc.GSCodeNarcPlugin'
        }
    }
}

repositories {
    maven {
        url 'https://repo.ronella.xyz/artifactory/java-central'
        credentials {
            username "${artifactoryUsername}"
            password "${artifactoryPassword}"
        }
        mavenContent {
            releasesOnly()
        }
    }
    maven {
        url 'https://repo.ronella.xyz/archiva/repository/snapshots/'
        credentials {
            username "${archivaUsername}"
            password "${archivaPassword}"
        }
        mavenContent {
            snapshotsOnly()
        }
    }
}

dependencies {
    implementation gradleApi()
    implementation localGroovy()

    testImplementation gradleTestKit()
    testImplementation 'org.spockframework:spock-core:1.3-groovy-2.5'
    testImplementation 'junit:junit:4.12'
}

compileGroovy.finalizedBy(jar)

publishing {
    publications {
        repositories.maven {
            url "https://repo.ronella.xyz/archiva/repository/${publishingPath}/"
            credentials {
                username "${archivaUsername}"
                password "${archivaPassword}"
            }
        }
    }
}